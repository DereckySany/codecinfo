apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.parseus.codecinfo"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 18
        versionName "2.2.0"
        resConfigs "en"

        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [appName:"android.app.Application"]
    }
    buildTypes {
        debug {
            versionNameSuffix "-dev"
            variantFilter { variant ->
                def names = variant.flavors*.name
                if (!names.contains("tv")) {
                    multiDexEnabled true
                    manifestPlaceholders = [appName:"androidx.multidex.MultiDexApplication"]
                }
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "app", "platform"

    productFlavors {
        nonFree {
            dimension "app"

            dependenciesInfo {
                includeInApk = false
            }
        }

        standard {
            dimension "app"

            dependenciesInfo {
                includeInApk = false
            }
        }

        mobile {
            dimension "platform"
            versionCode 10000 + android.defaultConfig.versionCode
        }

        tv {
            dimension "platform"
            minSdkVersion 21
            versionCode 20000 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/*.version'
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("nonFree") && names.contains("tv")) {
            setIgnore(true)
        }
    }
}

configurations {
    mobileDebugImplementation {}
    nonFreeMobileImplementation {}
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.3.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.core:core-ktx:1.5.0-beta01"
    implementation 'androidx.preference:preference-ktx:1.1.1'

    implementation "com.squareup.leakcanary:plumber-android:$leakCanary_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.okio:okio:2.10.0"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary_version"

    tvImplementation 'androidx.leanback:leanback:1.1.0-beta01'
    tvImplementation 'androidx.leanback:leanback-preference:1.1.0-beta01'

    mobileDebugImplementation 'androidx.multidex:multidex:2.0.1'
    mobileImplementation 'com.github.ditacristianionut:AppInfoBadge:1.3'
    mobileImplementation 'com.google.android.material:material:1.3.0'

    nonFreeMobileImplementation fileTree(include: ['*.jar'], dir: 'libs')
    nonFreeMobileImplementation 'com.google.android.play:core:1.9.1'
    nonFreeMobileImplementation ('com.google.android.play:core-ktx:1.8.1') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
}